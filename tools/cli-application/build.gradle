import java.nio.file.Paths

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id "org.jetbrains.kotlin.kapt" version "1.3.72"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.palantir.graal'

graal {
    mainClass 'io.vrap.rmf.codegen.cli.MainKt'
    outputName 'rmf-codegen'
}

publishing {
    publications {
        Maven(MavenPublication) {
            publication -> project.shadow.component(publication)
        }
    }
}

String dir = "${buildDir.toString()}/gensrc/main/kotlin/io/vrap/rmf/codegen/cli/info"

dependencies {
    compile project(':languages:javalang:builder-renderer:java-builder-client')
    compile project(':languages:typescript')
    compile project(':languages:postman')
    compile project(':languages:php')
    compile project(':languages:ramldoc')

    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'com.google.guava:guava:29.0-jre'
    compile 'info.picocli:picocli:4.2.0'
    kapt 'info.picocli:picocli-codegen:4.2.0'
    compile group: 'io.methvin', name: 'directory-watcher', version: '0.9.9'
    implementation "io.reactivex.rxjava3:rxjava:3.0.3"
}



shadowJar {
    manifest {
        attributes 'Main-Class': 'io.vrap.rmf.codegen.cli.MainKt'
    }
    append "plugin.properties"
    archiveFileName = 'rmf-gen.jar'
    minimize()
    destinationDirectory = file("../..")
}

task generateBuildInfo {
    doFirst {
        mkdir(dir)
        file(Paths.get("$dir/BuildInfo.kt")).text = """
package io.vrap.rmf.codegen.cli.info

object BuildInfo {
    const val VERSION = "$version"
}
"""

    }
}

compileKotlin.dependsOn generateBuildInfo
tasks.build.dependsOn tasks.shadowJar

sourceSets.main.kotlin.srcDirs += "${buildDir}/gensrc/main/kotlin/"
