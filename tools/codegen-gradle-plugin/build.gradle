plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.20'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'com.gradle.plugin-publish'

dependencies {
    compile project(':languages:javalang:java-renderer')
    compile project(':languages:javalang:builder-renderer:java-builder-client')
    compile project(':languages:typescript')
    compile project(':languages:postman')
    compile project(':languages:python')
    compile project(':languages:php')
    compile project(':languages:csharp')
    compile project(':languages:ramldoc')
    compile project(':languages:oas')

    compile gradleApi()
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile 'junit:junit:4.13'
    testCompile gradleTestKit()
    testCompile "org.spockframework:spock-core:${versions.spock}"
    testCompile "org.assertj:assertj-core:3.16.0"
}


gradlePlugin {
    plugins {
        gradlePlugin {
            id = 'com.commercetools.rmf.codegen-plugin'
            implementationClass = 'io.vrap.rmf.codegen.plugin.RamlCodegenPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/commercetools/rmf-codegen'
    vcsUrl = 'https://github.com/commercetools/rmf-codegen/tree/main/tools/codegen-gradle-plugin'
    plugins {
        gradlePlugin {
            id = 'com.commercetools.rmf.codegen-plugin'
            displayName = 'Raml codegenerator'
            description = 'This a code generator for the ramle specification, the supported language for this version is java'
            tags = ['RAML', 'java']
        }
    }
}

task createClasspathManifest {
    def outputDir = file("$buildDir/testClasspath")
    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir
    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-under-test-metadata.properties").text = "implementation-classpath="+sourceSets.main.runtimeClasspath.join(":")
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}

compileJava.finalizedBy(createClasspathManifest)
